***********************************************************************
* DESCRIPTION:                                                        *
* ___________________________________________________________________ *
* CALCULATES_THE_CURRENT_DAY_OF_THE_WEEK_____________________________ *
* ___________________________________________________________________ *
* ___________________________________________________________________ *
* ENTRY:____R0:__@WORKAREA___________________________________________ *
* ___________________________________________________________________ *
* ___________________________________________________________________ *
* ___________________________________________________________________ *
* RETURN:   R15: RETURN CODE_________________________________________ *
* __________R1:__DAY_OF_THE_WEEK_____________________________________ *
* _______________0_-->_Sunday________________________________________ *
* _______________1_-->_Monday________________________________________ *
* _______________2_-->_Tuesday_______________________________________ *
* _______________3_-->_Wednesday_____________________________________ *
* _______________4_-->_Thursday______________________________________ *
* _______________5_-->_Friday________________________________________ *
* _______________6_-->_Saturday______________________________________ *
* ___________________________________________________________________ *
***********************************************************************
           TITLE    'TAFWEEKD GET DAY OF WEEK'
           ENTRY    TAFWEEKD
TAFWEEKD   RSECT    ,                              READ ONLY CSECT
TAFWEEKD   AMODE    31
TAFWEEKD   RMODE    ANY
***********************************************************************
* Register
***********************************************************************
R0         EQU      0
R1         EQU      1                                                   
R2         EQU      2                                                   
R3         EQU      3                                                   
R4         EQU      4                                                   
R5         EQU      5                                                   
R6         EQU      6                                                   
R7         EQU      7                                                   
R8         EQU      8                                                   
R9         EQU      9                                                   
R10        EQU      10
R11        EQU      11                             @WORKAREA            
R12        EQU      12                             @TAFWEEKD            
R13        EQU      13                             LINKAGE              
R14        EQU      14                             LINKAGE              
R15        EQU      15                             LINKAGE              
***********************************************************************
* ENTRY
***********************************************************************
           SAVE     (14,12)                        SAVE CALLER REGISTER
           LR       R12,R15                  R12:  BASE REGISTER
           USING    TAFWEEKD,R12
           LR       R11,R0                   R11:  @WORKAREA
***********************************************************************
* CREATE WORKAREA
***********************************************************************
           XC       0(WORKAREAL,R11),0(R11)        RESET TO ZERO
           USING    WORKAREA,R11
           ST       R13,SAVEAREA+4
           LA       R15,SAVEAREA
           ST       R15,8(R13)
           LR       R13,R15                  R13:  @SAVEAREA
***********************************************************************
* MAIN
***********************************************************************
*--------------------------------------------------
*          GET DATE OF TODAY
*--------------------------------------------------
           TIME     DEC,DATPACK,                                       +
               ZONE=LT,                                                +
               DATETYPE=DDMMYYYY,                                      +
               LINKAGE=SYSTEM,MF=(E,TIME01)
           MVI      DAY+7,X'0C'
           LLC      R1,DATPACK+8
           STC      R1,DAY+6
           MVO      DAY+4(4),DAY+4(3)
           MVI      MONTH+7,X'0C'
           LLC      R2,DATPACK+9
           STC      R2,MONTH+6
           MVO      MONTH+4(4),MONTH+4(3)
           MVI      YEAR+7,X'0C'
           LLH      R3,DATPACK+10
           STH      R3,YEAR+5
           MVO      YEAR+2(6),YEAR+2(5)
*--------------------------------------------------
*          WEEKDAY CALCULATION
*--------------------------------------------------
           CVB      R1,MONTH
           CVB      R0,YEAR
           CIJL     R1,3,MONTH_LT_2
           ST       R1,H
           ST       R0,K
           J        WEEKDAY
MONTH_LT_2 AHI      R1,12
           ST       R1,H
           AHI      R0,-1
           ST       R0,K
WEEKDAY    DS       0H
           LR       R3,R1
           MHI      R3,3
           AHI      R3,3
           IILF     R4,5
           SR       R2,R2
           DLR      R2,R4
           CVB      R15,DAY
           LR       R14,R1
           SLL      R14,1
           ALR      R3,R14
           ALR      R3,R15
           ALR      R3,R0
           AHI      R3,1
           LR       R2,R0
           SRL      R2,2
           ALR      R3,R2
           LR       R5,R0
           IILF     R6,100
           SR       R4,R4
           DLR      R4,R6
           SLR      R3,R5
           LR       R5,R0
           IILF     R6,400
           SR       R4,R4
           DLR      R4,R6
           ALR      R3,R5
           IILF     R7,7
           SR       R2,R2
           DLR      R2,R7
***********************************************************************
* RETURN
***********************************************************************
EXIT01     DS       0H
           L        R13,4(R13)               R13:  CALLER SAVEAREA      00070000
           L        R14,12(,R13)             R14:  RESTORE RETURN ADDR.
           SR       R15,R15                  R15:  RC=0
           LR       R1,R2                    R1:   RETURN DAY OF WEEK
           RETURN   (2,12),RC=(15)                 RETURN TO CALLER     00140007
***********************************************************************
* CONSTANTS
***********************************************************************
           LTORG    ,
***********************************************************************
* WORKAREA
***********************************************************************
WORKAREA   DSECT    ,                              MY WORKAREA
SAVEAREA   DS       18F                            MY SAVEAREA
DATPACK    DS       PL16
DAY        DS       D
MONTH      DS       D
YEAR       DS       D
H          DS       F
K          DS       F
TIME01     TIME     LINKAGE=SYSTEM,MF=L
WORKAREAL  EQU      *-WORKAREA                     SIZE OF MY WORKAREA
***********************************************************************
* DSECTS
***********************************************************************
           END
