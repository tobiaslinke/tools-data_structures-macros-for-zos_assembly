***********************************************************************
* DESCRIPTION                                                         *
* ___________________________________________________________________ *
* ENTRY:    R1:  @PARMS______________________________________________ *
* _______________PARM1_@STRING_______________________________________ *
* _______________PARM2_STRING LENGTH_________________________________ *
* _______________PARM3_@DELIMITER____________________________________ *
* _______________PARM4_DELIMITER COUNT_______________________________ *
* __________R0:__@SPLITSTR_WORKAREA__________________________________ *
* ___________________________________________________________________ *
* RETURN:   R1:  @ARRAY______________________________________________ *
* __________R15: RC__________________________________________________ *
* ___________________________________________________________________ *
***********************************************************************
           TITLE    'SPLITSTR SPLIT A STRING'
SPLITSTR   CSECT    ,
SPLITSTR   AMODE    31
SPLITSTR   RMODE    ANY
***********************************************************************
* REGISTER
***********************************************************************
R0         EQU      0
R1         EQU      1                                                   
R2         EQU      2                                                   
R3         EQU      3                                                   
R4         EQU      4                                                   
R5         EQU      5                                                   
R6         EQU      6                                                   
R7         EQU      7                                                   
R8         EQU      8                                                   
R9         EQU      9                                                   
R10        EQU      10
R11        EQU      11                                                  
R12        EQU      12                                                  
R13        EQU      13                                                  
R14        EQU      14                                                  
R15        EQU      15                                                  
***********************************************************************
* ENTRY
***********************************************************************
           SAVE     (14,12)                        SAVE CALLER REGS
           LR       R12,R15                  R12:  BASE REGISTER
           USING    SPLITSTR,R12
           LR       R3,R1                    R3:   @PARMS
           LR       R11,R0                   R11:  @WORKAREA
***********************************************************************
* CREATE WORKAREA
***********************************************************************
           USING    WORKAREA,R11                   ADDRESS WORKAREA
           XC       SAVEAREA,SAVEAREA
           XC       TRANSTBL,TRANSTBL
           ST       R13,SAVEAREA+4                 BACK LINK
           LA       R15,SAVEAREA                   MY SAVEAREA
           ST       R15,8(R13)                     FORWARD LINK
           LR       R13,R15                  R13:  @SAVEAREA
***********************************************************************
* MAIN
***********************************************************************
           L        R4,8(,R3)                R4:   @DELIMITERS
           L        R5,12(,R3)               R5:   NUMBER DELIMITERS
           LA       R1,TRANSTBL              R1:   TRANSLATION TABLE
*----------------------------------------------------------------------
*          SET ALL  DELIMITER IN CONVTABLE
*----------------------------------------------------------------------
STORE      DS       0H
           LLC      R2,0(,R4)                R2:   DELIMITER
           STC      R2,0(R2,R1)                    SET DELIMITER IN TBL
           LA       R4,1(,R4)                R4:   @NEXT DELIMITER
           JCT      R5,STORE                       FOR ALL DELIMITERS
*----------------------------------------------------------------------
*          ALLOC STORAGE FOR ARRAY
*----------------------------------------------------------------------
           IILF     R1,#ARRAYL+(255*#ARRAYELL)     DEFAULT 255 ELEMENTS 
           STORAGE  OBTAIN,LOC=(31,31),LENGTH=(R1),BNDRY=DBLWD          
           LR       R9,R1                    R9:   @ARRAY               
           USING    #ARRAY,R9                      ADDRESS ARRAY HEADER 
           ST       R0,#ARRAYSIZ                   SET SIZE IN ARRAY    
           LA       R10,#ARRAYL(,R9)         R10:  @ARRAY INDEX 0       
           USING    #ARRAYEL,R10                   ADDRESS ARRAY INDEX  
           L        R5,4(,R3)                      STRING LENGTH
           L        R4,0(,R3)                R4:   @STRING
           ST       R4,#ARRAYELM@                  SET PTR TO SUBSTRING
           LR       R7,R4                    R7:   @LAST SUBSTRING
           IILF     R8,1                     R8:   ARRAY ELEMENTS
*----------------------------------------------------------------------
*          FIND ALL DELIMITERS
*----------------------------------------------------------------------
           LA       R1,TRANSTBL              R1:   @TRANSTBL
CONTINUE   TRTE     R4,R15,B'0000'                 FIND DELIMITER
           JC       1,CONTINUE                     256 BYTES PROCESSED
           JC       8,ATEND
*----------------------------------------------------------------------
*          SET LENGTH OF SUBSTRING
*----------------------------------------------------------------------
           LR       R6,R4                    R6:   @DELIMITER
           SLR      R6,R7                    R6:   LENGTH SUBSTR
           CIJE     R5,1,CONT1
           AHI      R8,1
           CRJE     R4,R7,ATBEGIN
CONT1      ST       R6,#ARRAYELML                  SET LENGTH SUBSTRING
*----------------------------------------------------------------------
*          SET ELEMENT FOR DELIMITER
*----------------------------------------------------------------------
           LA       R10,#ARRAYELL(,R10)      R10:  NEXT ARRAY ELEMENT
           LR       R7,R4                    R7H:  NEW @LAST SUBSTRING
           ST       R4,#ARRAYELM@                  SET @DELIMITER
           LA       R4,1(,R4)                R4:   @STRING AFTER DELIM.
           AHI      R5,-1
           CIJH     R5,0,CONTINUE
           AHI      R8,1
           J        ATEND                          SEARCH NEXT DELIM.
ATBEGIN    DS       0H
           IILF     R8,1
           LA       R4,1(,R4)
           AHI      R5,-1
           J        CONTINUE
*----------------------------------------------------------------------
*          SET LENGTH LAST ELEMENT AND NUM ELEMENTS
*----------------------------------------------------------------------
ATEND      DS       0H
           SLR      R4,R7                    R6:   LENGTH SUBSTR
           ST       R4,#ARRAYELML                  SET LENGTH SUBSTRING
           ST       R8,#ARRAYELM                   SAVE NUMBER ELEMENTS
***********************************************************************
* RETURN
***********************************************************************
EXIT01     DS       0H
           L        R13,4(R13)               R13:  CALLER SAVEAREA
           L        R14,12(,R13)             R14:  RETURN ADDRESS
           SR       R15,R15                  R15:  RC=0
           SR       R0,R0
           LR       R1,R9                    R3:   @ARRAY
           RETURN   (2,12),RC=(15)                 RETURN TO CALLER
***********************************************************************
* CONSTANTS
***********************************************************************
           LTORG    ,
***********************************************************************
* WORKAREA
***********************************************************************
WORKAREA   DSECT    ,
SAVEAREA   DS       18F
TRANSTBL   DS       XL256
WORKAREAL  EQU      *-WORKAREA
***********************************************************************
* DSECTS
***********************************************************************
           DATATYPE ,
           END      SPLITSTR
