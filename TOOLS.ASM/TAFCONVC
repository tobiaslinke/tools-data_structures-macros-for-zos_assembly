***********************************************************************
* DESCRIPTION                                                         *
* ___________________________________________________________________ *
* ENTRY:    R1:  FULLWORD NUMBER_____________________________________ *
* __________R0:  @TAFCONVC WORKAREA__________________________________ *
* ___________________________________________________________________ *
* RETURN:   R1:  LENGTH NUMBER_______________________________________ *
* __________R0:  @NUMBER_____________________________________________ *
* ___________________________________________________________________ *
***********************************************************************
           TITLE    'TAFCONVC CONVERT BIN NUMBER TO EBCDIC'
           ENTRY    TAFCONVC
TAFCONVC   CSECT    ,
TAFCONVC   AMODE    31
TAFCONVC   RMODE    ANY
***********************************************************************
* REGISTER
***********************************************************************
R0         EQU      0
R1         EQU      1                                                   
R2         EQU      2                                                   
R3         EQU      3                                                   
R4         EQU      4                              NUMBER               
R5         EQU      5                                                   
R6         EQU      6                                                   
R7         EQU      7                                                   
R8         EQU      8                                                   
R9         EQU      9                                                   
R10        EQU      10
R11        EQU      11                             @WORKAREA            
R12        EQU      12                             @TAFCONVC            
R13        EQU      13                             LINKAGE              
R14        EQU      14                             LINKAGE              
R15        EQU      15                             LINKAGE              
***********************************************************************
* ENTRY
***********************************************************************
           SAVE     (14,12)                        SAVE CALLER REGS
           LR       R12,R15                  R12:  BASE REGISTER
           USING    TAFCONVC,R12                   ADDRESS BASE
           LR       R4,R1                    R4:   NUMBER
           LR       R11,R0                   R11:  @TAFCONVH WORKAREA
***********************************************************************
* CREATE WORKAREA
***********************************************************************
           XC       0(WORKAREAL,R11),0(R11)
           USING    WORKAREA,R11
           ST       R13,SAVEAREA+4
           LA       R15,SAVEAREA
           ST       R15,8(R13)
           LR       R13,R15
***********************************************************************
* MAIN
***********************************************************************
           LA       R5,NUMCONV               R5:   @EBCDIC NUMBER
           CIJE     R4,0,NULL                      IF 0 RETURN X'F0'
           XC       0(16,R5),0(R5)                 RESET CONV AREA
           CVD      R4,NUMPACKED                   CONV NUMBER TO DEC.
           MVC      0(16,R5),EDITMASK              MOVE EDIT MASK
           ED       0(16,R5),NUMPACKED             EDIT NUMBER TO ZONED
           LA       R3,16(,R5)               R3:   @END NUMBER FIELD
           TRT      0(16,R5),CONVTBL               FIND 1. NON BLANK
           SLR      R3,R1                    R3:   LENGTH NUMBER CHAR
           LR       R4,R1                    R4:   @NUMBER CHAR
***********************************************************************
* RETURN
***********************************************************************
EXIT01     DS       0H
           L        R13,4(R13)               R13:  CALLER SAVEAREA
           L        R14,12(,R13)             R14:  RESTORE RETURN ADDR.
           LR       R1,R3                    R1:   LENGTH NUMBER CHAR
           LR       R0,R4                    R0:   @NUMBER CHAR
           SR       R15,R15                        RC=0
           RETURN   (2,12),RC=(15)                 RETURN TO CALLER
***********************************************************************
* SUBROUTINE
***********************************************************************
NULL       DS       0H
           MVI      0(R5),X'F0'                    SET CHAR 0
           LR       R4,R5                    R4:   @CHAR 0
           IILF     R3,1                     R3:   LENGTH CHAR 0
           J        EXIT01                         RETURN
***********************************************************************
* CONSTANTS
***********************************************************************
EDITMASK   DC       XL16'40202020202020202020202020202020'
CONVTBL    DC       256X'01'                       STOP AT 1. NON BLANK
           ORG      CONVTBL+C' '
           DC       X'00'
           ORG
           LTORG    ,
***********************************************************************
* WORKAREA
***********************************************************************
WORKAREA   DSECT    ,
SAVEAREA   DS       18F
NUMBER     DS       F
NUMBERL    DS       F
NUMPACKED  DS       PL8
NUMCONV    DS       CL16
WORKAREAL  EQU      *-WORKAREA
           END      TAFCONVC