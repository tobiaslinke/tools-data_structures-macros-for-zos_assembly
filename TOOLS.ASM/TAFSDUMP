***********************************************************************
* DESCRIPTION                                                         *
* ___________________________________________________________________ *
* DUMPS A CHUNK OF STORAGE___________________________________________ *
* ___________________________________________________________________ *
* ___________________________________________________________________ *
* ENTRY:___R1:_@PARMS LIST___________________________________________ *
* ______________PARM1 --> @STORAGE___________________________________ *
* ______________PARM2_-->_STORAGE LENGTH_____________________________ *
* ______________PARM3_-->_@DCB_______________________________________ *
* ___________________________________________________________________ *
***********************************************************************
           TITLE    'TAFSDUMP DUMP STORAGE'
           ENTRY    TAFSDUMP
TAFSDUMP   RSECT    ,
TAFSDUMP   AMODE    31
TAFSDUMP   RMODE    ANY
***********************************************************************
* register
***********************************************************************
R0         EQU      0
R1         EQU      1                                                   
R2         EQU      2                                                   
R3         EQU      3                                                   
R4         EQU      4                                                   
R5         EQU      5                                                   
R6         EQU      6                                                   
R7         EQU      7                                                   
R8         EQU      8                                                   
R9         EQU      9                                                   
R10        EQU      10
R11        EQU      11                             @WORKAREA            
R12        EQU      12                             @TAFSDUMP            
R13        EQU      13                             LINKAGE              
R14        EQU      14                             LINKAGE              
R15        EQU      15                             LINKAGE              
***********************************************************************
* ENTRY
***********************************************************************
           SAVE     (14,12)                        SAVE CALLER REGS
           LR       R12,R15                  R12:  BASE REGISTER
           USING    TAFSDUMP,R12
           LR       R5,R1                    R5:   @PARMS
***********************************************************************
* CREATE WORKAREA
***********************************************************************
           STORAGE  OBTAIN,LENGTH=WORKAREAL,LOC=31,BNDRY=DBLWD
           LR       R11,R1
           LR       R2,R11
           LR       R6,R0
           IILF     R3,WORKAREAL
           SR       R1,R1
           MVCL     R2,R0
           USING    WORKAREA,R11
           ST       R13,SAVEAREA+4
           LA       R15,SAVEAREA
           ST       R15,8(R13)
           LR       R13,R15
           ST       R6,WORKSIZE
***********************************************************************
* MAIN
***********************************************************************
           L        R2,0(,R5)                R2:   @STORAGE
           L        R8,4(,R5)                R8:   LENGTH STORAGE
           L        R3,8(,R5)                R3:   @DCB
           MVC      SPRINTTXT(255),BLANK
           MVC      SPRINTTXT(L'DUMPCOL1),DUMPCOL1
           MVC      SPRINTTXT+45(16),DUMPCOL2
           LA       R9,DUMP004
           CIJE     R3,0,DUMP_WTO
           PUT      (R3),SPRINTTXT
DUMP004    ST       R2,ADDR
DUMP04     DS       0H
           MVC      SPRINTTXT(255),BLANK
           XC       TEMPTXT,TEMPTXT
           MVC      TEMPTXT(4),ADDR
           CHI      R8,16
           JH       DUMP05
           AHI      R8,-1
           EX       R8,TEMPTXTMVC
           AHI      R8,1
           TAFCONVH SRC=TEMPTXT,SRCL=20,TGT=SPRINTTXT,MF=(E,CONV)
           MVC      SPRINTTXT+45(16),TEMPTXT+4
           MVC      TEMPTXT(8),SPRINTTXT
           MVI      TEMPTXT+8,C' '
           MVC      TEMPTXT+9(8),SPRINTTXT+8
           MVI      TEMPTXT+17,C' '
           MVC      TEMPTXT+18(8),SPRINTTXT+16
           MVI      TEMPTXT+26,C' '
           MVC      TEMPTXT+27(8),SPRINTTXT+24
           MVI      TEMPTXT+35,C' '
           MVC      TEMPTXT+36(8),SPRINTTXT+32
           MVC      SPRINTTXT(45),TEMPTXT
           MVI      SPRINTTXT+44,C' '
           LA       R9,DUMP005
           CIJE     R3,0,DUMP_WTO
           PUT      (R3),SPRINTTXT
DUMP005    J        DUMP07
DUMP05     DS       0H
           MVC      TEMPTXT+4(16),0(R2)
           TAFCONVH SRC=TEMPTXT,SRCL=20,TGT=SPRINTTXT,MF=(E,CONV)
           MVC      SPRINTTXT+46(16),TEMPTXT+4
           MVC      TEMPTXT(8),SPRINTTXT
           MVI      TEMPTXT+8,C' '
           MVC      TEMPTXT+9(8),SPRINTTXT+8
           MVI      TEMPTXT+17,C' '
           MVC      TEMPTXT+18(8),SPRINTTXT+16
           MVI      TEMPTXT+26,C' '
           MVC      TEMPTXT+27(8),SPRINTTXT+24
           MVI      TEMPTXT+35,C' '
           MVC      TEMPTXT+36(8),SPRINTTXT+32
           MVI      TEMPTXT+44,C' '
           MVC      SPRINTTXT(45),TEMPTXT
           LA       R9,DUMP06
           CIJE     R3,0,DUMP_WTO
           PUT      (R3),SPRINTTXT
DUMP06     DS       0H
           ASI      ADDR,16
           AHI      R8,-16
           AHI      R2,16
           J        DUMP04
DUMP07     DS       0H
***********************************************************************
* RETURN
***********************************************************************
EXIT01     DS       0H
           L        R13,4(R13)               R13:  CALLER SAVEAREA
           L        R6,WORKSIZE
           STORAGE  RELEASE,ADDR=(R11),LENGTH=(R6)
           SR       R15,R15                        RC = 0
           RETURN   (14,12),RC=(15)                RETURN TO CALLER
DUMP_WTO   DS       0H
           IILF     R4,80
           STH      R4,WTOPARM
           MVC      WTOD,WTO1                      NO, DEFAULT WTO
           SR       R0,R0                          RESET R0
           LA       R1,WTOPARM
           ST       R1,WTOD+4
           WTO      MF=(E,WTOD)                    DO THE WTO
           BR       R9
***********************************************************************
* CONSTANTS
***********************************************************************
           LTORG
@TAFCONVH  DC       V(TAFCONVH)
TEMPTXTMVC MVC      TEMPTXT+4(0),0(R2)
DUMPCOL1   DC       CL44'ADDRESS  0_-_-_3_ 4_-_-_7_ 8_-_-_B_ C_-_-_F_'
DUMPCOL2   DC       CL16'0---4---8---C---'
BLANK      DC       CL255' '
NULL16     DC       XL16'00'
@MSG       DC       A(MSG01)
MSG01      DC       AL1(L'TXT01)
TXT01      DC       C'$1'
WTO1       WTO      TEXT=,MF=L
WTO1l      EQU      *-WTO1
***********************************************************************
* WORKAREA
***********************************************************************
WORKAREA   DSECT    ,
SAVEAREA   DS       18F
WORKSIZE   DS       F
ADDR       DS       F
TEMPTXT    DS       CL255
WTOD       DS       CL(WTO1L)                      WTO PARMLIST
WTOPARM    DS       0H
           DS       H
SPRINTTXT  DS       CL255
CONV       TAFCONVH MF=L
WORKAREAL  EQU      *-WORKAREA
***********************************************************************
* DSECTS
***********************************************************************
           DATATYPE ,
           END